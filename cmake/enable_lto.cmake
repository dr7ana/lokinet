# -flto
include(CheckIPOSupported)

#if(WIN32 OR ANDROID OR IOS OR (APPLE AND (ARCH_TRIPLET MATCHES "^arm64")))
if(WIN32 OR ANDROID OR IOS OR APPLE)
  set(with_lto_default OFF)
else()
  set(with_lto_default ON)
endif()

option(WITH_LTO "enable lto on compile time" ${with_lto_default})
if(WITH_LTO)
  if(WIN32)
    message(FATAL_ERROR "LTO not supported on win32 targets, please set -DWITH_LTO=OFF")
  endif()
  check_ipo_supported(RESULT IPO_ENABLED OUTPUT ipo_error)
  if(IPO_ENABLED)
    message(STATUS "LTO enabled")
  else()
    message(WARNING "LTO not supported by compiler: ${ipo_error}")
  endif()
else()
  message(STATUS "LTO disabled")
  set(IPO_ENABLED OFF)
endif()

function(enable_lto)
  if(WITH_LTO AND IPO_ENABLED)
    set_target_properties(${ARGN} PROPERTIES INTERPROCEDURAL_OPTIMIZATION ON)
  endif()
endfunction()
